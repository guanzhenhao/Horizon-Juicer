;;;;;;;;;;;;;;;;;;;MASK1:磨削参数_外;;;;;;;;;;;;;;;;;;;;;;
//M(Mask1/$85020/"panel_1_1_chs.png"/)

	DEF VAR1=(I/*0=$85135,1=$85136,2=$85137,3=$85138,4=$85139/0/$85134,,,/WR1//"/NC/_N_NC_GD2_ACX/GRIND[1]"/0,0,0/10,10,60/);磨削类型
	DEF VAR2=(I/*0=$85014,1=$85013//$85162,$85162,,/WR1//"/NC/_N_NC_GD2_ACX/GRIND[2]"/100,10,40/140,10,50/);是否修整
	DEF VAR3=(I/*0=$85014,1=$85013//$85163,$85163,,/WR1//"/NC/_N_NC_GD2_ACX/GRIND[3]"/195,10,40/230,10,50/);是否对刀
	DEF VAR4=(I/*0=$85100,1=$85140//$85141,,,/WR1/"panel_1_1_chs.png"/"/NC/_N_NC_GD2_ACX/TOOL_SET[9]"/0,0,0/270,10,70);对刀类型(首次/二次)
	DEF VAR5=(I/*0=$85142,1=$85143//$85144,,,/WR1//"/NC/_N_NC_GD2_ACX/TOOL_SET[8]"/0,0,0/270,30,70);对刀方式(手动/自动)
	DEF VAR11=(I/*0=$85146,1=$85147,2=$85148//$85145,,,/WR2/"panel_1_3_chs.png"/"/NC/_N_NC_GD2_ACX/TOOL_SET[20]"/0,0,0/270,50,70/);选择对刀位置

	DEF VAR12=(R/-500,500//$85151,$85150,$85043,/WR2//"/NC/_N_NC_GD2_ACX/TOOL_SET[12]"/215,305,165/280,305,60/);测头接触位
	DEF VAR13=(R/-500,500//$85153,$85152,$85043,/WR2//"/NC/_N_NC_GD2_ACX/TOOL_SET[13]"/215,325,165/280,325,60/);砂轮接触位
	DEF VAR15=(R/0,100//$85155,$85154,$85043,/WR2//"/NC/_N_NC_GD2_ACX/TOOL_SET[19]"/215,345,165/280,345,60/);工件槽深

	DEF PIECE_VOLUME=(I/*0=$85166,1=$85167//$85170,$85170,,/WR2/"panel_1_17_chs.png"/"/NC/_N_NC_GD2_ACX/INI[27]"/360,10,70/460,10,80/);单件/批量磨削方式选择
	DEF SCREW_R=(I/*0=$85124,1=$85125//$85103,$85103,,/WR2/"panel_1_4_chs.png"/"/NC/_N_NC_GD2_ACX/INI[1]"/360,40,70/460,40,60/);螺纹旋向
	DEF VAR6=(I/0,50//$85102,$85102,$85051,/WR2/"panel_1_5_chs.png"/"/NC/_N_NC_GD2_ACX/WORK[1]"/360,60,202/460,60,60/);工件头数
	DEF VAR7=(R3/0,50//$85104,$85104,$85043,/WR2/"panel_1_6_chs.png"/"/NC/_N_NC_GD2_ACX/INI[5]"/360,80,202/460,80,60/);工件螺距
	DEF VAR8=(R3/-2000,2000//$85108,$85108,$85043,/WR2/"panel_1_7_chs.png"/"/NC/_N_NC_GD2_ACX/INI[2]"/360,100,202/460,100,60/);工件左端
	DEF VAR9=(R3/-2000,2000//$85109,$85109,$85043,/WR2/"panel_1_8_chs.png"/"/NC/_N_NC_GD2_ACX/INI[3]"/360,120,202/460,120,60/);工件右端
	DEF VAR10=(R/-500,500//$85165,$85165,$85043,/WR2/"panel_1_9_chs.png"/"/NC/_N_NC_GD2_ACX/INI[11]"/360,150,202/460,150,60/);缓冲位置
	DEF X_QUIT=(R/-500,500//$85106,$85106,$85043,/WR2/"panel_1_10_chs.png"/"/NC/_N_NC_GD2_ACX/INI[24]"/360,170,202/460,170,60/);退刀位置
	DEF PITCH_COM=(R/-500,500//$85105,$85105,$85043,/WR2/"panel_1_11_chs.png"/"/NC/_N_NC_GD2_ACX/INI[25]"/360,190,202/460,190,60/);全长导程补偿
	
	DEF VAR20=(I/*0=$85014,1=$85013//$85159,$85159,,/WR2/"panel_1_12_chs.png"/"/NC/_N_NC_GD2_ACX/INI[12]"/360,270,80/405,270,30/);是否有锥度
	DEF VAR21=(I/0,10//$85160,,,$85052/WR2/"panel_1_13_chs.png"/"/NC/_N_NC_GD2_ACX/INI[13]"/0,270,0/460,270,75/);锥度分子
	DEF VAR22=(I/0,800//$85161,,,/WR2/"panel_1_14_chs.png"/"/NC/_N_NC_GD2_ACX/INI[14]"/0,0,0/490,270,30/);锥度分母

	DEF VAR23=(I/*0=$85014,1=$85013//$85164,$85164,,/WR2/"panel_1_18_chs.png"/"/NC/_N_NC_GD2_ACX/INI[39]"/360,290,80/405,290,30/);螺旋升角自动计算
	DEF VAR24=(I/0,45//$85055,,,$85042/WR2/"panel_1_19_chs.png"/"/NC/_N_NC_GD2_ACX/INI[18]"/0,290,0/460,290,75/);度
	DEF VAR25=(I/0,59//$85056,,,$85053/WR2/"panel_1_20_chs.png"/"/NC/_N_NC_GD2_ACX/INI[19]"/0,290,0/495,290,70/);分
	DEF VAR26=(I/0,59//$85057,,,$85054/WR2/"panel_1_21_chs.png"/"/NC/_N_NC_GD2_ACX/INI[20]"/0,290,0/525,290,70/);秒
	DEF WORK_MID_DIA=(R/0,200//$85171,$85171,$85043,/WR4/"panel_1_22_chs.png"/"/NC/_N_NC_GD2_ACX/INI[40]"/360,310,202/460,310,60/);工件中径

	DEF VAR27=(I/0,500///WR4//"$MN_USER_DATA_INT[0]"/0,0,0/0,0,0/);润滑间歇时间调用值
	DEF VAR28=(I/0,500///WR4//"$MN_USER_DATA_INT[1]"/0,0,0/0,0,0/);润滑启动时间调用值
	DEF VAR29=(I/0,500//$85127,$85127,$85048,/WR2/"panel_1_15_chs.png"/"/NC/_N_NC_GD2_ACX/INI[22]"/380,330,182/460,330,60/);润滑间歇时间输入值
	DEF VAR30=(I/0,500//$85126,$85126,$85047,/WR2/"panel_1_16_chs.png"/"/NC/_N_NC_GD2_ACX/INI[23]"/380,350,182/460,350,60/);润滑启动时间输入值

	DEF VAR16=(R///$85156,$85156,$85043,/WR1//"$AC_DRF[X]"/10,290,210/120,290,60/);DRF_X
	DEF VAR17=(R///$85157,$85157,$85043,/WR1//"$AC_DRF[Z]"/10,310,210/120,310,60/);DRF_Z
	DEF VAR19=(R///$85158,$85158,$85043,/WR1//"/NC/_N_NC_GD2_ACX/PROCESS[5]"/10,330,210/120,330,60/);当前磨削接触位
	DEF WHELL_POS_INI=(R///$85169,$85169,$85043,/WR1//"/NC/_N_NC_GD2_ACX/PROCESS[14]"/10,350,210/120,350,60/);初始磨削接触位(对刀完成后磨削接触位)

	DEF QCHECK=(I////WR4//"/Plc/Q113.5"/0,0,0/0,0,0);循环启动Q点检测
	
	DEF VAR14=(R///,,,/WR4//"/Plc/DB4900.DBX0.0"/0,0,0/0,0,0/);TEMP
	DEF VAR18=(R///,,,/WR4//"/Plc/DB4900.DBX1.0"/0,0,0/0,0,0/);TEMP

	HS1=($85001,ac7,se3);磨削参数
	HS2=($85002,ac7,se1);工艺参数
	HS3=($85003,ac7,se1);修整参数
	HS8=($85005,ac7,se1);返回

	VS1=("")

	PRESS(HS1)
		LM("MASK1")
	END_PRESS

	PRESS(HS2)
		LM("MASK2")
	END_PRESS

	PRESS(HS3)
		LM("MASK3")
	END_PRESS

	PRESS(HS8)
		EXIT
	END_PRESS

	CHANGE(VAR1)
		IF VAR1.VAL==1
			LM("MASK18")
		ENDIF
	END_CHANGE

	CHANGE(VAR14);由面板按键判断修整相关参数的显示
		IF VAR14.VAL==1
			VAR2.VAL=1;修整
		ELSE
			VAR2.VAL=0;不修整
		ENDIF
	END_CHANGE

	CHANGE(VAR18)
		IF VAR18.VAL==1;对刀按键有效
			VAR3.VAL=1
		ELSE
			VAR3.VAL=0;对刀按键无效
		ENDIF
	END_CHANGE

	CHANGE(VAR3)
		call("UP1")
	END_CHANGE

	CHANGE(VAR6)
		call("UP3")
	END_CHANGE

	CHANGE(VAR7)
		call("UP3")
	END_CHANGE

	CHANGE(WORK_MID_DIA)
		call("UP3")
	END_CHANGE

	CHANGE(VAR4);由对刀类型(首次/二次),判断对刀方式(手动/自动)
		call("UP1")
	END_CHANGE

	CHANGE(VAR5);由对刀方式(手动/自动),判断显示
		call("UP1")
	END_CHANGE

	CHANGE(VAR20)
		call("UP2")
	END_CHANGE
	
	CHANGE(VAR23)
		call("UP2")
		call("UP3")
	END_CHANGE
	
	CHANGE(QCHECK)
		call("UP2")
	END_CHANGE

	CHANGE(VAR29);转换润滑时间的输入值
		VAR27=VAR29
		VAR28=VAR30*100
	END_CHANGE

	CHANGE(VAR30);转换润滑时间的输入值
		VAR27=VAR29
		VAR28=VAR30*100
	END_CHANGE

	SUB(UP1)
		IF VAR3.VAL==0;不对刀
			VAR4.WR=1;首次二次
			VAR5.WR=1;手动自动
			IF VAR5.VAL==0
				VAR11.WR=1;对刀位置
				VAR12.WR=4
				VAR13.WR=4
				VAR15.WR=4
			ELSE
				VAR11.WR=4
				VAR12.WR=1
				VAR13.WR=1
				VAR15.WR=1
			ENDIF
		ELSE;对刀
			VAR4.WR=2;首次二次
			IF VAR4.VAL==0;
				VAR5.WR=2;手动自动
				IF VAR5.VAL==0;
					VAR11.WR=2
					VAR12.WR=4
					VAR13.WR=4
					VAR15.WR=4
				ELSE;选择自动对刀
					VAR11.WR=4
					VAR12.WR=2
					VAR13.WR=2
					VAR15.WR=2
				ENDIF
			ELSE;选择二次对刀
				VAR5.WR=1;对刀方式只能是手动
				VAR5.VAL=0
				VAR11.WR=2
				VAR12.WR=4
				VAR13.WR=4
				VAR15.WR=4
			ENDIF
		ENDIF
	END_SUB

	SUB(UP2)
		IF QCHECK.VAL==1
			SCREW_R.WR=1
			VAR6.WR=1
			VAR7.WR=1
			VAR8.WR=1
			VAR9.WR=1
			VAR10.WR=1
			X_QUIT.WR=1
			PITCH_COM.WR=1
			PIECE_VOLUME.WR=1
			VAR20.WR=1
			VAR21.WR=1
			VAR22.WR=1
			VAR23.WR=1
			IF VAR23.VAL==0
				VAR24.WR=1
				VAR25.WR=1
				VAR26.WR=1
				WORK_MID_DIA.WR=4
			ELSE
				VAR24.WR=1
				VAR25.WR=1
				VAR26.WR=1
				WORK_MID_DIA.WR=1
			ENDIF
		ELSE
			SCREW_R.WR=2
			VAR6.WR=2
			VAR7.WR=2
			VAR8.WR=2
			VAR9.WR=2
			VAR10.WR=2
			X_QUIT.WR=2
			PITCH_COM.WR=2
			PIECE_VOLUME.WR=2
			VAR20.WR=2
			IF VAR20.VAL==0
				VAR21.WR=1
				VAR22.WR=1
			ELSE
				VAR21.WR=2
				VAR22.WR=2
			ENDIF
			VAR23.WR=2
			IF VAR23.VAL==0
				VAR24.WR=2
				VAR25.WR=2
				VAR26.WR=2
				WORK_MID_DIA.WR=4
			ELSE
				VAR24.WR=1
				VAR25.WR=1
				VAR26.WR=1
				WORK_MID_DIA.WR=2
			ENDIF
		ENDIF
	END_SUB

	SUB(UP3)
		IF VAR23.VAL==1
			IF WORK_MID_DIA.VAL>0
				VAR24.VAL=SDEG(ATAN((VAR6.VAL*VAR7.VAL),(PI*WORK_MID_DIA.VAL)))
				VAR25.VAL=(SDEG(ATAN((VAR6.VAL*VAR7.VAL),(PI*WORK_MID_DIA.VAL)))-VAR24.VAL)*60
				VAR26.VAL=((SDEG(ATAN((VAR6.VAL*VAR7.VAL),(PI*WORK_MID_DIA.VAL)))-VAR24.VAL)*60-VAR25.VAL)*60
			ENDIF
		ENDIF
	END_SUB

//END
